plugins {
    id 'java'
    id 'application'
}

group = 'org.cli'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Test).configureEach {
    jvmArgs = ['-Dfile.encoding=UTF-8']
}


dependencies {
    implementation("org.jcommander:jcommander:2.0")
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

application {
    mainClass = 'org.cli.Main'
}

jar {
    manifest {
        attributes 'Main-Class': 'org.cli.Main'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName = 'cli'
    archiveVersion = '1.0'
}

tasks.register('createCliScript') {
    doLast {
        def cliScript = new File(project.rootDir, "cli")
        cliScript.text = '''#!/bin/bash
java -jar build/libs/cli-1.0.jar "\\$@"
'''
        cliScript.setExecutable(true)
    }
}

build.finalizedBy createCliScript
